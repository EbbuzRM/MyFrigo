// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath('com.android.tools.build:gradle')
    classpath('com.facebook.react:react-native-gradle-plugin')
    classpath('org.jetbrains.kotlin:kotlin-gradle-plugin')
  }
}

def reactNativeAndroidDir = new File(
  providers.exec {
    workingDir(rootDir)
    commandLine("node", "--print", "require.resolve('react-native/package.json')")
  }.standardOutput.asText.get().trim(),
  "../android"
)

allprojects {
  repositories {
    maven {
      // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
      url(reactNativeAndroidDir)
    }

    google()
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
  }
  configurations.all {
    resolutionStrategy {
      force 'androidx.test:runner:1.5.2'
      force 'androidx.test.espresso:espresso-core:3.5.1'
      force 'androidx.test.espresso:espresso-contrib:3.5.1'
      force 'androidx.test.espresso:espresso-idling-resource:3.5.1'
      force 'androidx.test.espresso:espresso-intents:3.5.1'
    }
  }
  afterEvaluate { project ->
    if (project.hasProperty('android')) {
      android {
        packagingOptions {
          pickFirst 'META-INF/LICENSE.md'
          pickFirst 'META-INF/LICENSE'
          pickFirst 'META-INF/NOTICE'
          pickFirst 'META-INF/LICENSE-notice.md'
        }
      }
    }
  }
}

apply plugin: "expo-root-project"
apply plugin: "com.facebook.react.rootproject"
